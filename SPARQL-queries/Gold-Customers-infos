PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX : <http://www.semanticweb.org/user/ontologies/2021/1/e-commerce#>

SELECT DISTINCT ?user_account ?name ?birthday ?gender ?country
(COUNT (?buy) AS ?n_buy) (SUM (?spent) AS ?total_spent)
WHERE {
#Gather info about people and accounts
?user_account a foaf:OnlineEcommerceAccount.
?user a foaf:Person;
foaf:name ?name;
foaf:birthday ?birthday;
foaf:gender ?gender;
foaf:account ?user_account.
#Buying activity of these users
?buy a :ProductBuying;
:performedByUser ?user_account;
:totalImport ?spent;
:deliveryLocation ?delivery_location.
?delivery_location :locatedInCity ?city.
?city :locatedInCountry ?country. }
GROUP BY ?user_account ?name ?birthday ?gender ?country
ORDER BY DESC (?total_spent)
